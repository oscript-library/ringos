#Использовать asserts

#Использовать "../../command"
#Использовать "../../../internal/ringmodules"
#Использовать "../../../internal/utils"
#Использовать "../../../internal/localization"

#Область ОписаниеПеременных

Перем Лог; // Объект для логирования событий
Перем ЛокализованныеСтроки; // Локализованные строки

Перем ПараметрыКомандыRing;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Возвращает версию установленной утилиты ring
//
//  Возвращаемое значение:
//   Строка - Версия утилиты
//
Функция Версия() Экспорт
	
	Команда = Новый КомандаRing();
	Если ТипЗнч(ПараметрыКомандыRing) = Тип("ПараметрыКомандыRing") Тогда
		Команда.УстановитьПараметрыКоманды(ПараметрыКомандыRing);
	КонецЕсли;
	
	Команда.ДобавитьПараметр("--version");
	
	Лог.Отладка(ЛокализованныеСтроки.ОтладкаЗапускаетсяКоманда, "version");
	Команда.Исполнить();
	Лог.Отладка(ЛокализованныеСтроки.ОтладкаВыполниласьКоманда, "version");
	
	Версия = Команда.ПолучитьВывод();
	
	Лог.Отладка(ЛокализованныеСтроки.ОтладкаВерсия, Версия);
	
	Возврат Версия;
	
КонецФункции

// Возвращает список установленных модулей утилиты ring
//
//  Возвращаемое значение:
//   СписокМодулейRing - Список модулей
//
Функция УстановленныеМодули() Экспорт
	
	Команда = Новый КомандаRing();
	Если ТипЗнч(ПараметрыКомандыRing) = Тип("ПараметрыКомандыRing") Тогда
		Команда.УстановитьПараметрыКоманды(ПараметрыКомандыRing);
	КонецЕсли;
	
	Команда.ДобавитьПараметр("help");
	Команда.ДобавитьПараметр("modules");
	
	Лог.Отладка(ЛокализованныеСтроки.ОтладкаЗапускаетсяКоманда, "help modules");
	Команда.Исполнить();
	Лог.Отладка(ЛокализованныеСтроки.ОтладкаВыполниласьКоманда, "help modules");
	
	УстановленныеМодулиСтрокой = Команда.ПолучитьВывод();
	
	УстановленныеМодули = Новый СписокМодулейRing(УстановленныеМодулиСтрокой);
	
	Лог.Отладка(ЛокализованныеСтроки.ОтладкаКоличествоМодулей, УстановленныеМодули.Количество());
	
	Возврат УстановленныеМодули;
	
КонецФункции

// Устнавливает параметры выполнения команд ring
//
// Параметры:
//   ПараметрыКоманды - ПараметрыКомандыRing - параметры выполнения команд ring
//
Процедура УстановитьПараметрыКоманды(ПараметрыКоманды) Экспорт
	
	// BSLLS:NestedFunctionInParameters-off
	Утверждения.ПроверитьРавенство(ТипЗнч(ПараметрыКоманды), Тип("ПараметрыКомандыRing"),
			ЛокализованныеСтроки.ИсключениеТипПараметрыКомандыRing);

	Утверждения.ПроверитьЗаполненность(ПараметрыКоманды,
			ЛокализованныеСтроки.ИсключениеЗаполненностьПараметрыRing);
	// BSLLS:NestedFunctionInParameters-on
	
	ПараметрыКомандыRing = ПараметрыКоманды;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта()
	
	Лог = ПараметрыПриложения.Лог("ring");
	ЛокализованныеСтроки = ПараметрыПриложения.ЛокализованныеСтроки(Локализация_МенеджерRing);
	
	ПараметрыКомандыRing = Неопределено;
		
КонецПроцедуры

#КонецОбласти